
( towerBlockHeight towerNumberOfBlocks * ) "towerHeight" store

( currentTableZ -1 * towerHeight +  towerBlockHeight - ) "towerTopZ" store

( currentTableZ -1 * towerBlockHeight 8 * +  0.2 + ) "towerSafeZ" store

( 

0 "towerNumberOfBlocks" store
0.044 "towerBlockHeight" store

30000 sceneSetDiscrepancySearchDepth 


leftOrRightArm "right" =
( 
 0.525866 -0.390611 towerSafeZ -0.001222 0.999998 0.001162 -0.001101 createEEPose "towerPose" store
)
( 
leftOrRightArm "left" = 
(
 0.524217 0.363860 towerSafeZ -0.001253 0.999999 -0.000147 0.000237 createEEPose "towerPose" store
)
 ( "Neither left or right arm." leftOrRighArm pauseStackExecution ) 
ifte
)
ifte


) "towerInit" store


( 
  setGraspModeTo3D
  halfImpulse
  goHome 
  tableMapBestAndPick 

  0.01 setGridSize
  0.05 setSpeed
  ( zUp waitUntilAtCurrentPosition ) 40 replicateWord

  currentPose  towerSafeZ setEEPosePZ moveEeToPoseWord waitUntilAtCurrentPosition
) "towerGetBlock" store

(
  fullImpulse towerPose moveEeToPoseWord waitUntilAtCurrentPosition 
  quarterImpulse towerPose towerTopZ 0.15  +  setEEPosePZ moveEeToPoseWord
  waitUntilAtCurrentPosition comeToStop
) "towerMoveAboveTower" store

(
  towerMoveAboveTower
  /* pressUntilEffortInit pressUntilEffort  */

  0.01 setGridSize
  ( zDown waitUntilAtCurrentPosition ) 5 replicateWord

  setMovementStateToMoving  
  comeToStop 
  0.005 setGridSize
  0.05 setSpeed
  ( zDown waitUntilAtCurrentPosition ) 3 replicateWord
  setMovementStateToMoving
  comeToStop 
  setMovementStateToMoving
  comeToStop 

  openGripper waitUntilGripperNotMoving 
  0.01 setGridSize ( zUp ) 10 replicateWord 

  waitUntilAtCurrentPosition

  towerNumberOfBlocks 1 + "towerNumberOfBlocks" store
) "towerPutBlockOnTower" store

(
  ( towerGetBlock towerPutBlockOnTower ) 8 replicateWord
) "towerMakeTower" store


towerInit
